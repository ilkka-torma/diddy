-- Jeandel-Rao tile set: aperiodic set of 11 Wang tiles
%nodes N E S W
%alphabet 0 1 2 3 4
%topology
up (0,0;N) (0,1;S);
dn (0,0;S) (0,-1;N);
rt (0,0;E) (1,0;W);
lt (0,0;W) (-1,0;E)
%SFT JeandelRao ACo
o.N = o.N.up & o.E = o.E.rt & (
-- 1131
(o.E=1 & o.N=1 & o.W=3 & o.S=1) |
-- 1232
(o.E=1 & o.N=2 & o.W=3 & o.S=2) |
-- 3133
(o.E=3 & o.N=1 & o.W=3 & o.S=3) |
-- 2421
(o.E=2 & o.N=4 & o.W=2 & o.S=1) |
-- 2220
(o.E=2 & o.N=2 & o.W=2 & o.S=0) |
-- 0001
(o.E=0 & o.N=0 & o.W=0 & o.S=1) |
-- 3102
(o.E=3 & o.N=1 & o.W=0 & o.S=2) |
-- 0212
(o.E=0 & o.N=2 & o.W=1 & o.S=2) |
-- 1214
(o.E=1 & o.N=2 & o.W=1 & o.S=4) |
-- 3312
(o.E=3 & o.N=3 & o.W=1 & o.S=2) |
-- 0131
(o.E=0 & o.N=1 & o.W=3 & o.S=1)
)
%tiler JeandelRao x_size=30 y_size=20 node_offsets={N:[1/2 3/4] E:[3/4 1/2] S:[1/2 1/4] W:[1/4 1/2]}
-- The following will run forever
--%empty JeandelRao