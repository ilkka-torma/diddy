-- Hat tiling

-- Hexagon
--   1         1
-- 6   2  R  2   6
-- 5   3     3   5 
--   4         4

%nodes 1 2 3 4 5 6

-- Hat tile
-- 
--    R1
--       R2
--       R3
--    R4
--

%alphabet Ha Hb Hc Hd He Hf Hg Hh Ra Rb Rc Rd Re Rf Rg Rh

-- Topology
--
--  N  = (0,1)
--  nE = (1,0)

%SFT hat_tiling ACn
(n.1 = Ha <-> n.2 = Hb <-> n.3 = Hc <-> n.4 = Hd <-> n.(0,1).5 = He <-> n.(0,1).4 = Hf <-> n.(1,0).6 = Hg <-> n.(1,0).1 = Hh)
&
(n.2 = Ha <-> n.3 = Hb <-> n.4 = Hc <-> n.5 = Hd <-> n.(1,0).6 = He <-> n.(1,0).5 = Hf <-> n.(1,-1).1 = Hg <-> n.(1,-1).2 = Hh)
&
(n.3 = Ha <-> n.4 = Hb <-> n.5 = Hc <-> n.6 = Hd <-> n.(1,-1).1 = He <-> n.(1,-1).6 = Hf <-> n.(0,-1).2 = Hg <-> n.(0,-1).3 = Hh)
&
(n.4 = Ha <-> n.5 = Hb <-> n.6 = Hc <-> n.1 = Hd <-> n.(0,-1).2 = He <-> n.(0,-1).1 = Hf <-> n.(-1,0).3 = Hg <-> n.(-1,0).4 = Hh)
&
(n.5 = Ha <-> n.6 = Hb <-> n.1 = Hc <-> n.2 = Hd <-> n.(-1,0).3 = He <-> n.(-1,0).2 = Hf <-> n.(-1,1).4 = Hg <-> n.(-1,1).5 = Hh)
&
(n.6 = Ha <-> n.1 = Hb <-> n.2 = Hc <-> n.3 = Hd <-> n.(-1,1).4 = He <-> n.(-1,1).3 = Hf <-> n.(0,1).5 = Hg <-> n.(0,1).6 = Hh)

&

(n.1 = Ra <-> n.6 = Rb <-> n.5 = Rc <-> n.4 = Rd <-> n.(-1,1).3 = Re <-> n.(-1,1).4 = Rf <-> n.(-1,0).2 = Rg <-> n.(-1,0).1 = Rh)
&
(n.6 = Ra <-> n.5 = Rb <-> n.4 = Rc <-> n.3 = Rd <-> n.(-1,0).2 = Re <-> n.(-1,0).3 = Rf <-> n.(0,-1).1 = Rg <-> n.(0,-1).6 = Rh)
&
(n.5 = Ra <-> n.4 = Rb <-> n.3 = Rc <-> n.2 = Rd <-> n.(0,-1).1 = Re <-> n.(0,-1).2 = Rf <-> n.(1,-1).6 = Rg <-> n.(1,-1).5 = Rh)
&
(n.4 = Ra <-> n.3 = Rb <-> n.2 = Rc <-> n.1 = Rd <-> n.(1,-1).6 = Re <-> n.(1,-1).1 = Rf <-> n.(1,0).5 = Rg <-> n.(1,0).4 = Rh)
&
(n.3 = Ra <-> n.2 = Rb <-> n.1 = Rc <-> n.6 = Rd <-> n.(1,0).5 = Re <-> n.(1,0).6 = Rf <-> n.(0,1).4 = Rg <-> n.(0,1).3 = Rh)
&
(n.2 = Ra <-> n.1 = Rb <-> n.6 = Rc <-> n.5 = Rd <-> n.(0,1).4 = Re <-> n.(0,1).5 = Rf <-> n.(-1,1).3 = Rg <-> n.(-1,1).2 = Rh)

--& n.1 != Ra & n.2 != Ra & n.3 != Ra & n.4 != Ra & n.5 != Ra & n.6 != Ra

%SFT empty Ao o!=o
--%contains @verbose empty hat_tiling

-- The following takes a few minutes to deduce
%tiler x_size=25 y_size=25 node_offsets={1:[1/2 5/6] 2:[3/4 2/3] 3:[3/4 1/3] 4:[1/2 1/6] 5:[1/4 1/3] 6:[1/4 2/3]} gridmoves=[[1 0] [1/2 1]] colors={Ha:[160 0 0] Hb:[160 0 0] Hc:[160 0 0] Hd:[160 0 0] He:[160 0 0] Hf:[160 0 0] Hg:[160 0 0] Hh:[160 0 0] Ra:[0 255 0] Rb:[0 255 0] Rc:[0 255 0] Rd:[0 255 0] Re:[0 255 0] Rf:[0 255 0] Rg:[0 255 0] Rh:[0 255 0]} 
hat_tiling


--colors={Ha:[0 0 255] Hb:[0 255 255] Hc:[160 0 0] Hd:[160 0 0] He:[160 0 0] Hf:[160 0 0] Hg:[160 0 0] Hh:[160 0 0] Ra:[0 255 0] Rb:[255 255 0] Rc:[160 0 0] Rd:[160 0 0] Re:[160 0 0] Rf:[160 0 0] Rg:[160 0 0] Rh:[160 0 0]}
